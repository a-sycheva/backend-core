plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'jacoco'
    id 'com.bakdata.mockito' version '1.9.0'
}

check.dependsOn checkstyleMain

group = 'ru.mentee.power'
version = '1.0-SNAPSHOT'

ext {
    junitVersion = '5.11.0'
    assertjVersion = '3.26.3'
    lombokVersion = '1.18.42'  // Sprint 1, BCORE-4
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

application {
    mainClass = 'ru.mentee.power.Main'

}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testImplementation 'org.mockito:mockito-core:5.14.2'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.14.2'

    implementation 'net.bytebuddy:byte-buddy:1.17.6'
    implementation 'net.bytebuddy:byte-buddy-agent:1.17.6'

    testImplementation 'net.bytebuddy:byte-buddy:1.17.6'
    testImplementation 'net.bytebuddy:byte-buddy-agent:1.17.6'

}

checkstyleMain {
    group = 'verification'
}

checkstyleTest {
    group = 'verification'
}

checkstyle {
    toolVersion = '10.12.4'  // актуальная версия на 2025 год для Gradle 9.1
    maxErrors = 0      // даже 1 ошибка (severity=error) → BUILD FAILED
    maxWarnings = 999  // предупреждения (severity=warning) не блокируют сборку
    showViolations = true
    configFile = file('config/checkstyle/checkstyle.xml')

}

jacoco {
    toolVersion = '0.8.14'  // актуальная версия на 2025 год с поддержкой Java 25
}

test {
    useJUnitPlatform()
}

// Настройка JaCoCo отчёта
jacocoTestReport {
    dependsOn test  // отчёт генерируется после выполнения тестов
    reports {
        xml.required = false
        csv.required = false
        html.required = true
        html.outputLocation = layout.buildDirectory.dir('reports/jacoco/test/html')
    }

    // Исключаем Main из отчёта покрытия
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect { dir ->
            fileTree(dir: dir, exclude: [
                    '**/Main.class'
            ])
        }))
    }
}

// Настройка проверки покрытия (минимум 80%)
jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule {
            limit {
                minimum = 0.80  // 80% покрытия инструкций
            }
        }
    }

    // Исключаем Main из отчёта покрытия
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect { dir ->
            fileTree(dir: dir, exclude: [
                    '**/Main.class'
            ])
        }))
    }
}

// Интеграция проверок в lifecycle
check {
    dependsOn jacocoTestCoverageVerification
}